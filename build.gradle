buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
    maven {
        name = "thiakil"
        url = "http://maven.thiakil.com/"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = "0.7.14-alpha"
group = "gigaherz.elementsofpower" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ElementsOfPower-1.12"

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "stable_39"

    replace "@VERSION@", project.version
    replaceIn "ElementsOfPower.java"
}

configurations {
    // configuration that holds jars to embed inside the jar
    embed
    embed.transitive = false
}

dependencies {
    compile "org.jetbrains:annotations:15.0"

    deobfCompile "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"
    embed "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"

    deobfCompile "gigaherz.guidebook:Guidebook-1.12.2:2.5.0"

    deobfCompile "mezz.jei:jei_1.12.2:4.10.0.199:api"
    runtime "mezz.jei:jei_1.12.2:4.10.0.199"

    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2:api"
    runtime "com.azanor.baubles:Baubles:1.12-1.5.2"

    compile fileTree(dir: 'libs', include: '*.jar')
}

jar {
    into('/META-INF/libraries') {
        from configurations.embed
    }

    manifest {
        attributes([
                "ContainedDeps": configurations.embed.collect { it.getName() }.join(' '),
                "Maven-Artifact":"${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp': System.currentTimeMillis()
        ])
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
