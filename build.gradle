buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = "0.7.11-alpha"
group = "gigaherz.elementsofpower" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ElementsOfPower-1.11.2"

minecraft {
    version = "1.11.2-13.20.0.2228"
    runDir = "run"

    mappings = "snapshot_20170324"

    replace "@VERSION@", project.version
    replaceIn "ElementsOfPower.java"
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency(':Commons-1.11.2:'))
        exclude '*CraftTweaker*'
        exclude '*Baubles*'
    }
    relocate 'gigaherz.common', 'gigaherz.survivalist.shadow.common'
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
    archives sourceJar
}

dependencies {
    compile "org.jetbrains:annotations:15.0"

    deobfCompile "gigaherz.commons:Commons-1.11.2:0.5.2"
    deobfCompile "gigaherz.guidebook:Guidebook-1.11.0:1.3.1"

    deobfCompile "mezz.jei:jei_1.11.2:4.2.10.248:api"
    runtime "mezz.jei:jei_1.11.2:4.2.10.248"

    compile fileTree(dir: 'libs', include: '*.jar')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
